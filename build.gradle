allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: "com.jfrog.artifactory"
    apply plugin: 'maven-publish'

    group 'munch-data'
    version '4.5'
    ext.restfulVersion = '2.1.2'
    ext.fileVersion = '1.2.4'

    ext.guiceVersion = '4.2.0'
    ext.awsVersion = '1.11.313'
    ext.ccVersion = '2.5.0-SNAPSHOT'

    sourceCompatibility = 1.9
    targetCompatibility = 1.9
    compileJava.options.encoding = 'UTF-8'

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact(sourcesJar) {
                    classifier = 'sources'
                }
            }
        }
    }

    configurations.all { resolutionStrategy.cacheChangingModulesFor 0, 'seconds' }

    dependencies {
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.0.0'
        testCompile group: 'org.easytesting', name: 'fest-assert', version: '1.4'

        compile group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.1u2'
    }
}

configure(subprojects.findAll { it.name.matches("service|(corpus)(-.+)?") }) {
    apply plugin: 'application'
    apply plugin: 'com.bmuschko.docker-java-application'

    ext.dockerTag = rootProject.name + '/' + project.name + ':4.0'
    ext.dockerHost = '197547471367.dkr.ecr.ap-southeast-1.amazonaws.com/' + dockerTag

    docker {
        javaApplication {
            baseImage = 'openjdk:9-jdk-slim'
            maintainer = 'Fuxing'
            ports = [8000]
            tag = dockerTag
        }
    }

    task publishDocker {
        dependsOn 'clean'
        dependsOn 'dockerBuildImage'
        tasks.findByName('dockerBuildImage').mustRunAfter 'clean'

        doLast {
            exec { commandLine "docker", "tag", dockerTag, dockerHost }
            exec { commandLine "docker", "push", dockerHost}
        }
    }
}

buildscript {
    repositories {
        maven {
            url 'http://artifactory.fxloh.com/artifactory/munch-repos'
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
        }
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.6+"
        classpath 'com.bmuschko:gradle-docker-plugin:3.0.12'
    }
}

// https://www.jfrog.com/confluence/display/RTF/Gradle+Artifactory+Plugin
artifactory {
    contextUrl = "${artifactory_contextUrl}"
    publish {
        repository {
            repoKey = "munch-${project.version.endsWith('-SNAPSHOT') ? 'snapshot' : 'release'}"
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
        defaults {
            publications('mavenJava')
        }
    }
    resolve {
        repository {
            repoKey = 'munch-repos'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
    }
}