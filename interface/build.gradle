project('interface-airtable') {
    dependencies {
        compile project(':data:data-client')
        compile group: 'catalyst', name: 'catalyst-edit-hour-utils', version: ccVersion
        compile group: 'catalyst', name: 'corpus-framework-airtable', version: '2.5.0-SNAPSHOT'
        compile group: 'file-service', name: 'client', version: fileVersion

        compile group: 'com.amazonaws', name: 'aws-java-sdk-ssm', version: '1.11.273'
        compile group: 'com.google.inject', name: 'guice', version: guiceVersion

        compile group: 'com.vividsolutions', name: 'jts', version: '1.13'
    }

    apply plugin: 'application'
    mainClassName = 'munch.data.InterfaceModule'
}


configure(subprojects.findAll { it.name.matches("^interface-(airtable)") }) {
    apply plugin: 'com.bmuschko.docker-java-application'

    ext.dockerTag = dockerRegistryUrl + rootProject.name + '/' + project.name + ':'

    dockerDistTar {
        instruction {'RUN apt-get update'}
        instruction {'RUN apt-get -qq -y install curl'}
        instruction {'RUN rm -rf /var/lib/apt/lists/*'}
    }

    docker {
        javaApplication {
            baseImage = 'openjdk:9-jdk-slim'
            maintainer = 'Fuxing'
            ports = [8000, 7755]
            tag = dockerTag + dockerRegistryVersion
        }
    }

    task buildImage {
        dependsOn 'clean'
        dependsOn 'dockerBuildImage'
        tasks.findByName('dockerBuildImage').mustRunAfter 'clean'
    }

    task publishDocker {
        dependsOn 'buildImage'

        doLast {
            exec { commandLine "docker", "push", dockerTag + dockerRegistryVersion }
        }
    }
}
