dependencies {
    compile project(':data')
    compile group: 'munch.restful', name: 'restful-server', version: restfulVersion
    compile group: 'munch.restful', name: 'restful-server-dynamodb', version: restfulVersion
    compile group: 'com.amazonaws', name: 'aws-java-sdk-dynamodb', version: awsVersion

    // AWS Elasticsearch Service
    compile group: 'io.searchbox', name: 'jest', version: '5.3.2'
    compile group: 'com.amazonaws', name: 'aws-java-sdk-core', version: awsVersion
    compile group: 'vc.inreach.aws', name: 'aws-signing-request-interceptor', version: '0.0.16'

    compile group: 'com.google.inject', name: 'guice', version: guiceVersion

    testCompile project(':client')
}

apply plugin: 'application'
apply plugin: 'com.bmuschko.docker-java-application'

ext.dockerTag = dockerRegistryUrl + rootProject.name + '/' + project.name + ':'

mainClassName = 'munch.data.service.ServiceModule'

docker {
    javaApplication {
        baseImage = 'openjdk:9-jdk-slim'
        maintainer = 'Fuxing'
        ports = [8000]
        tag = dockerTag + version
    }
}

task buildImage {
    dependsOn 'clean'
    dependsOn 'dockerBuildImage'
    tasks.findByName('dockerBuildImage').mustRunAfter 'clean'

    doLast {
        exec { commandLine "docker", "tag", dockerTag + version, dockerTag + dockerRegistryVersion }
    }
}

task publishDocker {
    dependsOn 'buildImage'

    doLast {
        exec { commandLine "docker", "push", dockerTag + version }
        exec { commandLine "docker", "push", dockerTag + dockerRegistryVersion }
    }
}